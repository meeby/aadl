package AircraftSystem
public
	with FlightControlSystem;
	with AltitudeHeadingSystem;
	with ControlSurfaceActuator;
	with UserInterface;
	with FCSExecutionPlatform;

	system OurAircraft
	end OurAircraft;

	system implementation OurAircraft.impl
		subcomponents
			fcs: system FlightControlSystem::FlightControlSystem.impl;
			ahs: device AltitudeHeadingSystem::AltitudeHeadingSystem;
			csa: device ControlSurfaceActuator::ControlSurfaceActuator;
			fci: device UserInterface::FlightControlInterface;
			pfdLeft: device UserInterface::PrimaryFlightDisplay;
			pfdRight: device UserInterface::PrimaryFlightDisplay;
			hw: system FCSExecutionPlatform::Hardware;
			
		connections
			c_fci_fcs: port fci.fci_fcs -> fcs.fcs_fci;
			c_fcs_pfd_left: port fcs.fcs_pfd -> pfdLeft.pfd_fcs;
			c_fcs_pfd_right: port fcs.fcs_pfd -> pfdRight.pfd_fcs;
			c_ahs_fcs: port ahs.ahs_fcs -> fcs.fcs_ahs;
			c_fcs_csa: port fcs.fcs_csa -> csa.csa_fcs;

		flows
			Flow_FCI_PFD_Left: end to end flow fci.fci_source -> c_fci_fcs -> fcs.flow_fci_pfd -> c_fcs_pfd_left -> pfdLeft.pfd_sink {
				Latency => 50 ms .. 50 ms;
			};
			Flow_FCI_PFD_Right: end to end flow fci.fci_source -> c_fci_fcs -> fcs.flow_fci_pfd -> c_fcs_pfd_right -> pfdRight.pfd_sink {
				Latency => 50 ms .. 50 ms;
			};
			Flow_Request_Control_Wheel_Steering: end to end flow fci.fci_source -> c_fci_fcs -> fcs.flow_fci_csa {
				Latency => 50 ms .. 50 ms;
			};
			Flow_Cancel_Control_Wheel_Steering: end to end flow fci.fci_source -> c_fci_fcs -> fcs.flow_fci_csa -> c_fcs_csa -> csa.csa_sink {
				Latency => 50 ms .. 50 ms;
			};
	
	end OurAircraft.impl;

end AircraftSystem;