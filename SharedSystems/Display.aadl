package Display
public
	with SSThreads;

	system Display
		features
			dis_fcfi: in data port;
			dis_apsi: in data port;
			dis_pfd: out data port;

			-- Clear Flight Director
			fci_dis: in data port;
			apsi_dis: in data port; -- need to know if AP is engaged

		flows
			flow_fc_pfd: flow path dis_fcfi -> dis_pfd {
						Latency => 5 ms .. 5 ms;
					};
			flow_ap_pfd: flow path dis_apsi -> dis_pfd {
						Latency => 5 ms .. 5 ms;
					};

					-- Clear Flight Director
			flow_fci_pfd: flow path fci_dis -> dis_pfd {
						Latency => 5 ms .. 5 ms;
					};
	end Display;

	system implementation Display.impl
		subcomponents
			displayProcess: process DisplayProcess.impl;
			clearFlightDirectorProcess: process ClearFlightDirectorProcess.impl;
		connections
			AP_STATUS_CONN: port apsi_dis -> clearFlightDirectorProcess.getAPStatus;
			CLEAR_FD_CONN: port fci_dis -> clearFlightDirectorProcess.getClearFlightDirectorRequest;
	end Display.impl;

	process DisplayProcess
	end DisplayProcess;

	process implementation DisplayProcess.impl
		subcomponents
			displayThread: thread SSThreads::SendDataToDisplay;
	end DisplayProcess.impl;

	process ClearFlightDirectorProcess
		features
			getClearFlightDirectorRequest: in data port;
			getAPStatus: in data port;
			allowClearFlightGuidance: out data port;
			doNotAllowClearFlightGuidance: out data port;
	end ClearFlightDirectorProcess;

	process implementation ClearFlightDirectorProcess.impl
		subcomponents
			clearThread: thread SSThreads::ClearFlightDirectorFromDisplay;
			distinguishAPStatus: thread SSThreads::RequestAPStatusIndicator;

		connections
			C1: port getClearFlightDirectorRequest -> clearThread.receiveClearFlightDirectorRequest;
			C2: port getAPStatus -> distinguishAPStatus.receiveAPStatus;
			C3: port distinguishAPStatus.autoPilotIsEngaged -> clearThread.receiveAPEngaged;
			C5: port clearThread.sendRequestToClearFlightDirector -> allowClearFlightGuidance;
			C4: port distinguishAPStatus.autoPilotIsDisengaged -> clearThread.receiveAPDisengaged;
	end ClearFlightDirectorProcess.impl;

end Display;